apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"


android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "rs.ltt.android"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 5
        versionName "0.1.4"

        setProperty("archivesBaseName", "Ltt.rs-$versionName")

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        release {
            minifyEnabled true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    File signingProperties = new File("signing.properties")

    if (signingProperties.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(signingProperties))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                v2SigningEnabled true
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    //AndroidX
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appcompatVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.ext.lifecycleVersion"

    implementation "androidx.navigation:navigation-fragment:$rootProject.ext.navVersion"
    implementation "androidx.navigation:navigation-ui:$rootProject.ext.navVersion"

    implementation "androidx.paging:paging-common:$rootProject.ext.pagingVersion"
    implementation "androidx.paging:paging-runtime:$rootProject.ext.pagingVersion"


    implementation "androidx.room:room-runtime:$rootProject.ext.roomVersion"
    implementation "androidx.room:room-guava:$rootProject.ext.roomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.ext.roomVersion"

    implementation "androidx.recyclerview:recyclerview:$rootProject.ext.recyclerViewVersion"
    implementation "androidx.recyclerview:recyclerview-selection:$rootProject.ext.recyclerViewSelectionVersion"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.ext.swipeRefreshLayoutVersion"

    implementation "androidx.work:work-runtime:$rootProject.ext.workVersion"


    //Google
    implementation 'com.google.android.material:material:1.3.0-alpha04'


    //Third party
    implementation 'org.hsluv:hsluv:0.2'

    implementation 'com.github.tony19:logback-android:2.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'


    //Own libraries (libraries we wrote and can commit to)
    implementation ("rs.ltt.jmap:jmap-mua:$rootProject.ext.muaVersion") {
        changing = true
    }


    //Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestImplementation ("rs.ltt.jmap:jmap-mock-server:$rootProject.ext.muaVersion") {
        changing = true
    }
}
